plugins {
    id 'java'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

def libraryGroupId = 'com.reynke.sloud'
def libraryArtifactId = rootProject.name
def libraryVersion = '1.0.3'
def libraryMainClassName = 'CorePlugin'

group libraryGroupId
version libraryVersion

def mainClassName = group + '.' + libraryArtifactId + '.' + libraryMainClassName

repositories {
    mavenCentral()

    maven {
        url 'https://papermc.io/repo/repository/maven-public/'
    }

    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/sloud/database-utilities")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
        }
    }
}

configurations {
    // Configuration that holds jars to include in the jar
    extraLibs
}

dependencies {
    implementation group: 'io.papermc.paper', name: 'paper-api', version: '1.17-R0.1-SNAPSHOT'
    extraLibs group: 'com.google.inject', name: 'guice', version: '5.0.1'
    extraLibs group: 'com.reynke.sloud', name: 'databaseutilities', version: '1.0.5'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    configurations.implementation.extendsFrom(configurations.extraLibs)
}

jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name,
                'Implementation-Version': archiveVersion,
                'Main-Class': mainClassName
    }

    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/sloud/core")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
